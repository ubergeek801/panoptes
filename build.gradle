plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'com.google.cloud.tools.jib' version '1.4.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'org.slaq'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

def globalJvmArgs = ['-XX:+UseG1GC', '-XX:+UseStringDeduplication', '-XX:MaxGCPauseMillis=5000',
        '--add-modules', 'java.se', '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED',
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
        '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens', 'java.management/sun.management=ALL-UNNAMED',
        '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED']
def k8sJvmArgs = ['-Xmx3328m', '-Dhazelcast.jmx=true']
k8sJvmArgs.addAll(globalJvmArgs) 

repositories {
    mavenCentral()
}

ext {
    set('vaadinVersion', '13.0.11')
}

dependencies {
    implementation 'ch.qos.logback:logback-classic'
    implementation 'com.hazelcast:hazelcast'
    implementation 'com.hazelcast:hazelcast-kubernetes:1.5.1'
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    implementation 'io.hawt:hawtio-springboot:2.7.0'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.postgresql:postgresql:42.2.6'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

test {
    jvmArgs = globalJvmArgs
}

bootRun {
    jvmArgs = globalJvmArgs
}

jib {
    allowInsecureRegistries = true

    from {
        image = 'bellsoft/liberica-openjdk-alpine:12.0.2-aarch64'
    }

    to {
        image = 'ubergeek801/slaqworx'
    }

    container {
        jvmFlags = k8sJvmArgs
    }
}
