plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'com.google.cloud.tools.jib' version '1.4.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'org.slaq'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

def globalJvmArgs = ['--add-modules', 'java.se',
        '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED',
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
        '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens', 'java.management/sun.management=ALL-UNNAMED',
        '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED']
def k8sJvmArgs = ['-Xmx3G']
k8sJvmArgs.addAll(globalJvmArgs) 

repositories {
    mavenCentral()
}

ext {
    set('vaadinVersion', '13.0.11')
}

dependencies {
    implementation 'ch.qos.logback:logback-classic'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.hazelcast:hazelcast'
    implementation 'com.hazelcast:hazelcast-kubernetes:1.5.1'
    implementation 'com.vaadin:vaadin-spring-boot-starter'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

bootRun {
    jvmArgs = globalJvmArgs
}

jib {
    allowInsecureRegistries = true

    from {
        image = 'bellsoft/liberica-openjdk-alpine:12.0.2-aarch64'
    }

    to {
        image = 'ubergeek801/slaqworx'
    }

    container {
        jvmFlags = k8sJvmArgs
    }
}
